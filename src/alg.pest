WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "//" ~ ( !NEWLINE ~ ANY )* ~ NEWLINE | "/*" ~ ( !"*/" ~ ANY )* ~ "*/" }

file = { SOI ~ module* ~ EOI }

number = @{ ASCII_DIGIT+ }
name = @{ ASCII_ALPHA ~ ( ASCII_ALPHANUMERIC | "_" )* }
path = { name ~ ( "::" ~ name )* }

tree = { "[" ~ ( tree_path ~ ( "|" ~ tree_path )* )? ~ "]" }
tree_path = { tree_index ~ ( "," ~ tree_index )* }
tree_index = _{ number | tree_char | tree }
tree_char = { "f" | "t" }

variance = { ( "+" | "-" )? }

type_arg = { ( "<" ~ ty ~ ("," ~ ty)* ~ ">" )? }
type_par = { ( "<" ~ name ~ ("," ~ name)* ~ ">" )? }
type_gen = { ( "<" ~ type_variance ~ ("," ~ type_variance)* ~ ">" )? }
type_variance = { variance ~ name }
par = { "(" ~ ")" | pattern_tuple? }
of_type = { ( ":" ~ ty )? }
return_type = { ( "->" ~ ty )? }
clauses = { ("where")? }

ty = _{ ty_fn | ty_strong }
ty_strong = _{ ty_gen | ty_tuple }
ty_gen = { path ~ type_arg }
ty_tuple = { "(" ~ ty ~ ( "," ~ ty )* ~ ")" }
ty_fn = { ty_strong ~ "->" ~ ty }

pattern = _{ pattern_var | pattern_comp | pattern_atom | pattern_tuple }
pattern_var = { name ~ ":" ~ ty }
pattern_atom = { path ~ type_arg }
pattern_comp = { path ~ type_arg ~ pattern_tuple }
pattern_tuple = { "(" ~ pattern ~ ( "," ~ pattern )* ~ ")" }

exp = _{ exp_call | exp_lambda | exp_strong }
exp_strong = _{ exp_var | exp_tuple | exp_match }
exp_var = { path ~ type_arg }
exp_call = { exp_strong ~ (exp_tuple)+ }
exp_tuple = { "(" ~ exp ~ ( "," ~ exp )* ~ ")" }
exp_match = { "{" ~ exp_arm ~ ("," ~ exp_arm )* ~ ","? ~ "}" }
exp_arm = { pattern ~ "=>" ~ exp }
exp_lambda = { pattern_tuple ~ "->" ~ exp }

truth_dir = { "." | "~" }
truth_ref = { path ~ type_arg ~ exp_tuple? }
substitute = { truth_dir ~ truth_ref ~ tree }

proof = _{ proof_match | proof_forall | proof_forall_block | proof_sequence | proof_block }
proof_sequence = { truth_ref ~ substitute* }
proof_block = { "{" ~ proof_block_def ~ proof ~ "}" }
proof_block_def = { element* }
proof_forall = { "forall" ~ pattern_tuple ~ "->" ~ proof }
proof_forall_block = { "forall" ~ proof_closure }
proof_match = { "match" ~ exp ~ proof_closure }
proof_closure = { "{" ~ proof_arm ~ ( "," ~ proof_arm )* ~ ","? ~ "}" }
proof_arm = { pattern ~ "=>" ~ proof }

element = _{ "struct" ~ elm_struct | "enum" ~ elm_enum | "let" ~ elm_let | "fn" ~ (elm_func | elm_func_match) | "pf" ~ ( elm_proof | elm_proof_forall ) }
elm_struct = { name ~ type_gen ~ elm_struct_par ~ clauses }
elm_struct_par = { ty_tuple? }
elm_enum = { name ~ type_gen ~ clauses ~ "{" ~ elm_emum_variant ~ ("," ~ elm_emum_variant)* ~ ","? ~ "}" }
elm_emum_variant = { name ~ elm_struct_par }
elm_let = { name ~ type_par ~ of_type ~ clauses ~ "=" ~ exp }
elm_func = { name ~ type_par ~ pattern_tuple ~ return_type ~ clauses ~ "=" ~ exp }
elm_func_match = { name ~ type_par ~ return_type ~ clauses ~ exp_match }
elm_proof = { name ~ type_par ~ par ~ clauses ~ "=" ~ proof }
elm_proof_forall = { name ~ type_par ~ clauses ~ proof_closure }

module = _{ "mod" ~ elm_mod | "use" ~ elm_use | element }
elm_mod = { name ~ elm_mod_scope? }
elm_mod_scope = { "{" ~ element* ~ "}" }
elm_use = { path }
